<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
              "http://www.w3.org/TR/html4/loose.dtd">
<html>
  <head>
    <title>lhstml</title><meta content="text/html; charset=UTF-8"  http-equiv="Content-Type" />
  </head>
  <body>

<h1>rel-eval is a leazy relational algebra interpreter written in haskell.</h1>
</br>
<pre>Copyright (C) 2007  Daniel Waeber, Fabian Bieker

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as 
published by the Free Software Foundation, version 3 of the License.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
</pre>
<h1>TableOps</h1>
</br>
Class to encapsulte Primitive Relation Algebra Operations on Table
ADT. Default implemantations are provied, too.
</br>
</br>
<pre>> module TableOps (
>   TableOps(..),
>   testTableOps,
> )
> where
>
> import Primes
> import Table
> import Lib.AssertFun
> import qualified Data.List as List
> import Data.List ((\\))
</pre>
</br>
TODO: simplify class/type constraints
</br>
<pre>> class (Table tab l t, Type t, Literal l t, Eq tab, Eq l)
>	=> TableOps tab l t | tab -> l t where
</pre>
<h4>basic set operations</h4>
</br>
<pre>>	union :: tab -&gt; tab -&gt; tab
>	union = applyOnTableRows List.union
>	difference :: tab -> tab -> tab
>	difference = applyOnTableRows (\\)
>	intersect :: tab -> tab -> tab
>	intersect = applyOnTableRows List.intersect
</pre>
<h4>cross-join</h4>
</br>
<pre>>	cross :: tab -&gt; tab -&gt; tab
>	cross t1 t2 = foldRows (addRowsFrom t2) newEmpty t1
</pre>
(addRowsFrom t) is a high order function. It creates a function out of a table t'.
The function will take a row r and a table t, and appends each rows form t' to
r and puts them into t, resulting in a table where r is crossed with each row
of t.
</br>
<pre>>		where
>               addRowsFrom :: tab -> (Row l -> tab -> tab)
>               addRowsFrom t' r t = foldRows (cons . (r++)) t t'
>		newEmpty = mkTable (header t1 ++ header t2) []
</pre>
<h4>Selection</h4>
</br>
<pre>> 	select :: ((Row l) -&gt; Bool) -&gt; tab -&gt; tab
> 	select p tab = mkTable (header tab) (filter p (rows tab))
</pre>
<h4>Projection</h4>
</br>
</br>
<pre>>	projectUnsafe :: [ColumnName] -&gt; tab -&gt; tab
>	project :: [ColumnName] -> tab -> tab
>	projectUnsafe wanted tab = mkTable newHeader $ rows newTab
>		where
>               filterWanted = map snd . filter fst . zip [elem n wanted | n <- columnNames tab]
>               newHeader = filterWanted (header tab)
>      		newTab = mapRowsUnsafe filterWanted tab
</pre>
FIXME: throw away project and only use projectUnsave? -&gt; use [[]] and []
</br>
<pre>>       project wanted tab
>               | not checkExist = error(
>                   "invalid projection: a name can't be found inside the table in project" ++
>                   show wanted ++ " form " ++ show names)
>               | not checkMulti = error(
>                   "invalid projection: multiple rows with same name in project " ++
>                   show wanted ++ " form " ++ show names)
>               | otherwise = projectUnsafe wanted tab
>               where
>               names = columnNames tab
>               checkExist = null (wanted \\ names)
>               checkMulti = length wanted == 
>                   length [n | w <- wanted, n <- names, w==n]
</pre>
<h4>Renaming</h4>
</br>
Syntax: rename{Unsafe} [(oldName, newName)] table
</br>
<pre>>	renameUnsafe :: [(ColumnName,ColumnName)] -&gt; tab -&gt; tab
>	rename ::[(ColumnName,ColumnName)] -> tab -> tab
>	renameUnsafe names tab = mkTable (replaceNames h) (rows tab)
>		where
>		h = header tab
>		replaceNames = map (getNewName names)
>		getNewName [] header = header
>		getNewName ((old,new):xs) h@(name, ctype)
>			| name == old = (new, ctype)
>			| otherwise = getNewName xs h
</pre>
<pre>>	rename names tab
>		| checkParams = renameUnsafe names tab
>		| otherwise = error ("invalid rename: names = " ++ show names 
>			++ "\n table column names = " ++
>			 show columnNames)
>		where
>		h = header tab
>		checkParams = null (oldNames \\ columnNames)
>		(oldNames,_) = unzip names
>		(columnNames,_) = unzip h
</pre>
<h4>Advnced Operations</h4>
</br>
<pre>>       theta :: (Row l -&gt; Row l -&gt; Bool) -&gt; tab -&gt; tab -&gt; tab
>       theta f t1 t2 = mkTable newHeader
>		[ x++y | x <- r1, y <- r2, f x y]
>		where
>		newHeader = header t1 ++ header t2
>		r1 = rows t1
>		r2 = rows t2
</pre>
</br>
<pre>>	natural :: tab -&gt; tab -&gt; tab
</pre>
We can cons all falid rows into a new emptyTable.
</br>
<pre>>	natural tab1 tab2 = foldRows consRows (mkTable newHeader []) tab1
>		where
>               h1 = header tab1
>               h2 = header tab2
>               newHeader = List.union h1 h2
</pre>
To check a row, we create a new Row with each header inside, so we can check if
it is a functional relation.  if so, it is valid.
</br>
<pre>>               zipedRow r1 r2 = List.union (zip h1 r1) (zip h2 r2)
>               checkFunctional ziped = all
>                   (\h -> 1 == length [h | (h',_) <- ziped, h==h']) 
>                   newHeader 
</pre>
So we have to fold over each table, and check all rows inside the inner fold
</br>
<pre>>               consRows r tab = foldRows 
>                       (\r' t -> checkedCons (zipedRow r r') t) 
>                       tab tab2
>               checkedCons r = if checkFunctional r then cons $ map snd r else id
</pre>
</br>
TODO: Better implementaion of left right full outer joins
</br>
<pre>>       left :: (tab -&gt; tab -&gt; tab) -&gt; tab -&gt; tab -&gt; tab
>       left f tab1 tab2
>               | checkHeader = union original $ cross (notInOriginal tab1) (projectAway tab1 $ (nullTab tab2))
>               | otherwise = error("This function you tried to call as a left join "++
>                                   "can't be modefied to be become outer!")
>               where
>                       original = f tab1 tab2
>                       checkHeader = all (uncurry (==)) $ zip (header original) (header tab1)
>                       notInOriginal t = t \\\ projectTo t original
>                       projectTo t' t = project (columnNames t') t 
>
>                       projectAway t' t = project (columnNames t \\ columnNames t') t
>                       nullTab t = mkTable (header t) $ [map (\a -> getNull) (header t)]
</pre>
<pre>>       right :: (tab -&gt; tab -&gt; tab) -&gt; tab -&gt; tab -&gt; tab
>       right f tab1 tab2
>               | checkHeader = union original $ cross (notInOriginal tab2) (projectAway tab2 $ (nullTab tab1))
>               | otherwise = error("This function you tried to call as a right join "++
>                                   "can't be modefied to be become outer!")
>               where
>                       original = f tab1 tab2
>                       checkHeader = all (uncurry (==)) $ zip (header original) (header tab2)
>                       notInOriginal t = t \\\ projectTo t original
>                       projectTo t' t = natural (crossN (columnNames t') t) (project (columnNames t') t)
>                       crossN :: [String] -> tab -> tab
>                       crossN [] t = t
>                       crossN (n:ns) t = cross (project [n] t) (crossN ns t)
>
>                       projectAway t' t = project (columnNames t \\ columnNames t') t
>                       nullTab t = mkTable (header t) $ [map (\a -> getNull) (header t)]
</pre>
</br>
<h4>syntatic sugar</h4>
</br>
<pre>>       σ :: ((Row l) -&gt; Bool) -&gt; tab -&gt; tab
>       σ = select
>       π :: [ColumnName] -> tab -> tab
>       π = project
>       ρ :: [(ColumnName,ColumnName)] -> tab -> tab
>       ρ = rename
>       θ :: (Row l -> Row l -> Bool) -> tab -> tab -> tab
>       θ = theta
>	(&&&) :: tab -> tab -> tab
>	(&&&) = intersect
>	(|||) :: tab -> tab -> tab
>	(|||) = union
>	(***) :: tab -> tab -> tab
>	(***) = cross
>	(\\\) :: tab -> tab -> tab
>	(\\\) = difference
</pre>
Higher order function to apply f on the rows of two tables.
Checks if the headers of the table match, if not error is raised.
This is usefull to impl. union, intersection etc.
</br>
<pre>>	applyOnTableRows :: ([Row l] -&gt; [Row l] -&gt; [Row l]) -&gt; tab -&gt; tab -&gt; tab
>	applyOnTableRows f t1 t2
>		| h1 == h2 = mkTable h1 (f r1 r2)
>		| otherwise = error ("applyOnTableRows: table headers"
>			++ "do not match: " ++
>			(show $ columnNames t1) ++ " /= " ++ (show $ columnNames t1))
>		where
>		h1 = header t1
>		h2 = header t2
>		r1 = rows t1
>		r2 = rows t2
</pre>
</br>
<h2>Unit Test</h2>
</br>
TODO: write testTable + unit tests
</br>
<pre>> testTableOps = True</pre>
  </body>
</html>
