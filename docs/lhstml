#!/usr/bin/sed -f

1i\
<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"\
              "http://www.w3.org/TR/html4/loose.dtd">\
<html>\
  <head>\
    <title>lhstml</title><meta content="text/html; charset=UTF-8"  http-equiv="Content-Type" />\
  </head>\
  <body>\

# headlines
/^-\+$/d
s,^- \(.*\) -$,<h1>\1</h1>, 
s,^-- \(.*\) --$,<h2>\1</h2>, 
s,^--- \(.*\) ---$,<h3>\1</h3>, 
s,^---- \(.*\) ----$,<h3>\1</h3>, 

# lists
/^\* /{
    s,^\* ,<ul>\n<li>,
    :item
        :line
            n
            s,^\s,,
        tline
        s,^\* ,</li>\n<li>,
    titem
    s,^,</li></ul>\n,
}

# line
s,_\+,<hl>,

# code
/^>/{
    s,^>,<pre>\n>,
    :code
        n
        s,^>,>,
    tcode
    s,^$,</pre>\n,
}

# pre
/^  /{
    s,^  ,<pre>\n,
    :pre
        n
        s,^  ,,
    tpre
    s,^$,</pre>\n,
}

$a\
  </body>\
</html>
